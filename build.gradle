plugins {
    id 'ear'
}

group 'com.jfisherdev'

repositories {
    mavenCentral()
}

dependencies {
    deploy project(path: ':wf-ejbstartup-lock-commonejb', configuration: 'archives')
    deploy project(path: ':wf-ejbstartup-lock-web', configuration: 'archives')
    deploy project(path: ':wf-ejbstartup-lock-ejb2', configuration: 'archives')
    earlib project(':wf-ejbstartup-lock-commons')
}

ear {
    deploymentDescriptor {  // custom entries for application.xml:
        initializeInOrder = true
        module("wf-ejbstartup-lock-commonejb.jar", "ejb")
        webModule("wf-ejbstartup-lock-web.war", "/wfejbstartuplock")
        module("wf-ejbstartup-lock-ejb2.jar", "ejb")
    }
}

task deployStandalone(dependsOn: build) {
    doLast {
        String systemProperty = System.getProperty("wildfly.home")
        String envHome = System.getenv("WILDFLY_HOME") != null ? System.getenv("WILDFLY_HOME") : System.getenv("JBOSS_HOME")
        String wildflyHome = systemProperty != null ? systemProperty : envHome
        String deploymentsDir = wildflyHome + "/standalone/deployments"
        copy {
            from "$buildDir/libs/wf-ejbstartup-lock.ear"
            into deploymentsDir
        }
    }
}
